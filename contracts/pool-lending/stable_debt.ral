import "std/nft_interface"

Contract Debt (
    debtor: Address,
    loanAmount: U256,
    collateralAmount: U256,
    timeCreated: U256,
    interestRate: U256,
    parentContract: Address
) {
    pub fn getLoanAmount() -> U256 {
        return loanAmount
    }

    pub fn getCollateralAmount() -> U256 {
        return collateralAmount
    }

    pub fn getDebtor() -> Address {
        return debtor
    }

    pub fn getParentAddress() -> Address {
        return parentContract
    }

    pub fn calculateRepayment() -> (U256) {
        // Calculate elapsed time
        let elapsedTime = blockTimeStamp!() - timeCreated
    
        // Avoid integer truncation: multiply first, then divide
        let gain = (loanAmount * interestRate * elapsedTime) / (31556926000 * 10000)
    
        // Return the original amount + accrued interest + flat interest
        return loanAmount + gain
    }

    @using(checkExternalCaller = true, assetsInContract = true)
    pub fn destroyDebt(caller: Address) -> () {
        checkCaller!(callerAddress!() == parentContract, 0)

        destroySelf!(caller)
    }

    @using(updateFields = false, checkExternalCaller = true)
    pub fn updateDebtCode(newCode: ByteVec) -> () {
        checkCaller!(callerAddress!() == parentContract, 9)

        // added security param -> would stop parentContract / factory updates
        assert!(debtor == parentContract, 10)

        migrate!(newCode)
    }

    @using(updateFields = true, checkExternalCaller = true)
    pub fn updateDebtFields(newCode: ByteVec, immFields: ByteVec, mutFields: ByteVec) -> () {
        checkCaller!(callerAddress!() == parentContract, 9)

        // added security param -> would stop parentContract / factory updates
        assert!(debtor == parentContract, 10)

        migrateWithFields!(newCode, immFields, mutFields)
    }
}

TxScript UpdateDebtCode (
    debt: Debt,
    newCode: ByteVec
) {
    debt.updateDebtCode(newCode)
}

TxScript UpdateDebtFields (
    debt: Debt,
    newCode: ByteVec, 
    immFields: ByteVec, 
    mutFields: ByteVec
) {
    debt.updateDebtFields(newCode, immFields, mutFields)
}